apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  buildToolsVersion "$build_tools_version"

  lintOptions {
    abortOnError true
    explainIssues true
    ignoreWarnings true
    textReport true
    textOutput 'stdout'
    // Should try to remove last two here
    disable "MissingTranslation", "AppCompatCustomView", "InvalidPackage"
    // I really want some to show as errors
    error "InlinedApi", "StringEscaping"
  }

  defaultConfig {
    applicationId "com.nononsenseapps.feeder"
    versionCode 36
    versionName "1.6.8"
    compileSdkVersion 28
    minSdkVersion 18
    targetSdkVersion 28

    // For espresso tests
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    // Export Room schemas
    javaCompileOptions {
      annotationProcessorOptions {
        arguments = ["room.schemaLocation":
                         "$projectDir/schemas".toString()]
      }
    }
  }

  sourceSets {
    // To test Room we need to include the schema dir in resources
    androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
  }

  buildTypes {
    debug {
      minifyEnabled false
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }

  packagingOptions {
    // Rome incorrectly bundles stuff in its jar
    pickFirst "rome-utils-${rome_version}.jar"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

kotlin {
  experimental {
    coroutines "enable"
  }
}

dependencies {
  kapt "androidx.room:room-compiler:$room_version"
  implementation "androidx.room:room-runtime:$room_version"

  implementation "android.arch.work:work-runtime-ktx:$work_version"

  implementation "androidx.constraintlayout:constraintlayout:$constraintlayout_version"
  implementation "androidx.recyclerview:recyclerview:$recyclerview_version"
  implementation "androidx.legacy:legacy-support-v4:$legacy_support_version"
  implementation "androidx.appcompat:appcompat:$appcompat_version"
  implementation "androidx.preference:preference:$preference_version"
  implementation "com.google.android.material:material:$material_version"

  // ViewModel and LiveData
  implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

  // To support SDK18
  implementation "com.nononsenseapps:filepicker:4.1.0"
  // Better times
  implementation "joda-time:joda-time:2.3"
  // HTML parsing
  implementation "org.jsoup:jsoup:1.7.3"
  implementation "org.ccil.cowan.tagsoup:tagsoup:1.2.1"
  // RSS
  implementation "com.rometools:rome:$rome_version"
  implementation "com.rometools:rome-modules:$rome_version"
  // JSONFeed
  implementation project(":jsonfeed-parser")
  // For better fetching
  implementation "com.squareup.okhttp3:okhttp:3.10.0"
  // For supporting missing cyphers on older platforms
  implementation "org.conscrypt:conscrypt-android:1.1.2"
  // Image loading
  implementation "com.github.bumptech.glide:glide:3.7.0"
  implementation "com.github.bumptech.glide:okhttp3-integration:1.4.0@aar"


  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  // Coroutines
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:0.22"
  // For doing coroutines on UI thread
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22"
  // tests
  testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
  testImplementation "junit:junit:4.12"
  testImplementation "org.mockito:mockito-core:2.13.0"
  testImplementation "io.mockk:mockk:1.7.2"

  androidTestImplementation "androidx.recyclerview:recyclerview:$recyclerview_version"
  androidTestImplementation "androidx.legacy:legacy-support-v4:$legacy_support_version"
  androidTestImplementation "androidx.appcompat:appcompat:$appcompat_version"
  androidTestImplementation "com.google.android.material:material:$material_version"
  androidTestImplementation "junit:junit:4.12"
  androidTestImplementation "androidx.test:runner:$test_runner_version"
  androidTestImplementation "androidx.test:rules:$test_rules_version"
  androidTestImplementation "androidx.room:room-testing:$room_version"
}
